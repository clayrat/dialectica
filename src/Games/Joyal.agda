module Games.Joyal where

open import Prelude

open import Meta.Record

open import Data.Unit
open import Data.Empty
open import Data.Bool
open import Data.Sum

open import Games.Simple

open Game

private variable
  â„“ : Level

{- Jan's adaptation of Joyal's games -}

data GameJ (â„“ : Level) : ğ’° (â„“suc â„“) where
  done  : Prop â„“ â†’ GameJ â„“
  sigma : {X : ğ’° â„“} â†’ (X â†’ GameJ â„“) â†’ GameJ â„“
  pi    : {X : ğ’° â„“} â†’ (X â†’ GameJ â„“) â†’ GameJ â„“

{- Dialectica objects can be read as simple games involving
    a Ïƒ-move u âˆˆ U followed by a Ï€-move x âˆˆ X. -}

ofdial : Game â„“ â†’ GameJ â„“
ofdial A = sigma Î» u â†’ pi Î» x â†’ done (A .win u x)

{- Strategies and costrategies -}

stratJ : GameJ â„“ â†’ ğ’° â„“
stratJ {â„“} (done _)      = Lift â„“ âŠ¤
stratJ     (sigma {X} k) = Î£[ x ê‰ X ] stratJ (k x)
stratJ     (pi {X} k)    = (x : X) â†’ stratJ (k x)

costratJ : GameJ â„“ â†’ ğ’° â„“
costratJ {â„“} (done _)      = Lift â„“ âŠ¤
costratJ     (sigma {X} k) = (x : X) â†’ costratJ (k x)
costratJ     (pi {X} k)    = Î£[ x ê‰ X ] costratJ (k x)

winJ : {g : GameJ â„“} â†’ stratJ g â†’ costratJ g â†’ Prop â„“
winJ {g = done p}  _       _       = p
winJ {g = sigma k} (x , Ïƒ) Ï€       = winJ {g = k x} Ïƒ (Ï€ x)
winJ {g = pi k}    Ïƒ       (x , Ï€) = winJ {g = k x} (Ïƒ x) Ï€

{- The set of strategies and costrategies together with the payoff
    function can be used to define a Dialectica object. -}

dialof : GameJ â„“ â†’ Game â„“
dialof g .strat   = stratJ g
dialof g .costrat = costratJ g
dialof g .win     = winJ {g = g}

{- We can also define winning strategies in the same way. -}

wstratJ : GameJ â„“ â†’ ğ’° â„“
wstratJ (done w)      = âŒ w âŒŸ
wstratJ (sigma {X} k) = Î£[ x ê‰ X ] wstratJ (k x)
wstratJ (pi {X} k)    = (x : X) â†’ wstratJ (k x)

wsJ : {g : GameJ â„“} â†’ wstratJ g â†’ stratJ g
wsJ {g = done w}  _       = lift tt
wsJ {g = sigma k} (x , u) = x , wsJ u
wsJ {g = pi k}    u       = wsJ âˆ˜ u

wstrat-winning : (g : GameJ â„“) (u : wstratJ g) (c : costratJ g) â†’ âŒ winJ (wsJ u) c âŒŸ
wstrat-winning (done w)  u       _       = u
wstrat-winning (sigma k) (x , u) c       = wstrat-winning (k x) u (c x)
wstrat-winning (pi k)    u       (x , c) = wstrat-winning (k x) (u x) c

{- Negation -}

{- The complement of a game exchanges the roles of the two players. -}

oppJ : GameJ â„“ â†’ GameJ â„“
oppJ (done w)  = done (el! (Â¬ âŒ w âŒŸ))
oppJ (sigma k) = pi (oppJ âˆ˜ k)
oppJ (pi k)    = sigma (oppJ âˆ˜ k)

{- Binary product -}

prodJ : GameJ â„“ â†’ GameJ â„“ â†’ GameJ â„“
prodJ {â„“} g h = pi {X = Lift â„“ Bool} Î» where
  (lift false) â†’ g
  (lift true)  â†’ h

{- Tensor product -}

{- The tensor product of games lets them be played side by side.
   The player Ïƒ takes priority and move when either game is of the
   form [Î£i . Gi], but it must win both games to win overall.

   In this principle this can be defined in a straightforward way
   using a simple case analysis. Below I have elided the symmetric
   cases, and each line defines one layer of choice only, even when
   shown as âˆ‘ + âˆ‘.

       - âˆ‘iÂ·Gi âŠ— âˆ‘jÂ·Hj := âˆ‘iÂ·(Gi âŠ— âˆ‘jÂ·Hj) + âˆ‘jÂ·(âˆ‘iÂ·Gi âŠ— Hj)
       - âˆ‘iÂ·Gi âŠ— âˆjÂ·Hj := âˆ‘iÂ·(Gi âŠ— âˆjÂ·Hj)
       - âˆ‘iÂ·Gi âŠ— win   := âˆ‘iÂ·(Gi âŠ— win)
       - âˆ‘iÂ·Gi âŠ— lose  := âˆ‘iÂ·(Gi âŠ— lose)
       - âˆiÂ·Gi âŠ— âˆjÂ·Hj := âˆiÂ·(Gi âŠ— âˆjÂ·Hj) Ã— âˆjÂ·(âˆiÂ·Gi âŠ— Hj)
       - âˆiÂ·Gi âŠ— win   := âˆiÂ·(Gi âŠ— win)
       - âˆiÂ·Gi âŠ— lose  := âˆiÂ·(Gi âŠ— lose)
       -   win âŠ— win   := win
       -   win âŠ— lose  := lose
       -  lose âŠ— lose  := lose

   While straightforward in principle, it is somewhat tricky
   to define in a way which makes termination evident to Coq,
   because only one of the two games will have a move "peeled off"
   at any given step. To make this work we use a sort of continuation
   passing style where two fixpoints communicate with each other
   while peeling off exactly one move at a time.

   Unfortunately, under this approach, the symmetry of the underlying
   process is not reflected by the structure of the definition.
   In the definition of [G âŠ— H], we will use separate cases depending
   on the structure of [G]. -}

{- Tensoring with base games -}

{- If [G] is at its conclusion, we finish playing [H] largely as-is,
   but adjust its payoff based on the outcome of [G]. -}

tens-done : Prop â„“ â†’ GameJ â„“ â†’ GameJ â„“
tens-done gw (done hw) = done (el! (âŒ gw âŒŸ Ã— âŒ hw âŒŸ))
tens-done gw (sigma k) = sigma (tens-done gw âˆ˜ k)
tens-done gw (pi k)    = pi (tens-done gw âˆ˜ k)

{- Tensoring with Ïƒ-games -}

{- When [G] is of the form [âˆ‘iÂ·Gi], we must "mix in" any Ïƒ-moves
    that are available in [H] as well. The following definition helps. -}

sigma2 : {X Y : ğ’° â„“} â†’ (X â†’ GameJ â„“) â†’ (Y â†’ GameJ â„“) â†’ GameJ â„“
sigma2 {X} {Y} gk hk = sigma {X = X âŠ Y} [ gk , hk ]áµ¤

{- If Ïƒ chooses a move in [H], we continue with the corresponding
   subgame. When Ïƒ chooses (or is forced) to play in G, we invoke the
   corresponding "Gi continuation" and pass the residual [H]. -}

tens-sigma : {X : ğ’° â„“} â†’ (X â†’ GameJ â„“ â†’ GameJ â„“) â†’ GameJ â„“ â†’ GameJ â„“
tens-sigma gk h@(sigma hk) = sigma2 (Î» x â†’ gk x h) Î» x â†’ tens-sigma gk (hk x)
tens-sigma gk h            = sigma Î» x â†’ gk x h

{- Tensoring with Ï€-games -}

{- When [G] is of the form [âˆiÂ·Gi], we must first exhaust all
   possible Ïƒ-moves in [H]. Control transfers to Ï€ only once we
   encounter a Ï€-move in [H] as well, at which point we must mix in
   the available Ï€-moves from [G]. -}

pi2 : {X Y : ğ’° â„“} â†’ (X â†’ GameJ â„“) â†’ (Y â†’ GameJ â„“) â†’ GameJ â„“
pi2 {X} {Y} gk hk = pi {X = X âŠ Y} [ gk , hk ]áµ¤

{- Once again, when Ï€ chooses to move in [G], we invoke the
   corresponding continuation and pass to it the residual [H]. -}

tens-pi : {X : ğ’° â„“} â†’ (X â†’ GameJ â„“ â†’ GameJ â„“) â†’ GameJ â„“ â†’ GameJ â„“
tens-pi gk h@(done _)   = pi (Î» x â†’ gk x h)
tens-pi gk   (sigma hk) = sigma Î» x â†’ tens-pi gk (hk x)
tens-pi gk h@(pi hk)    = pi2 (Î» x â†’ gk x h) Î» x â†’ tens-pi gk (hk x)

{- Putting everything together -}

{- With this, we are ready to do the case analysis and recursion
    on the game [G]. Termination checking works because instead of
    [tens_sigma] and [tens_pi] invoking [tens] on the subgames of [G]
    directly, they do it through a continuation which is defined within
    the scope of [tens] itself. -}

tensJ : GameJ â„“ â†’ GameJ â„“ â†’ GameJ â„“
tensJ (done gw)  h = tens-done gw h
tensJ (sigma gk) h = tens-sigma (tensJ âˆ˜ gk) h
tensJ (pi gk)    h = tens-pi (tensJ âˆ˜ gk) h

{- Internal hom -}

{- As expected, we can now describe the game used to construct
   morphisms using a combination of tensors and negation. -}

homJ : GameJ â„“ â†’ GameJ â„“ â†’ GameJ â„“
homJ g h = oppJ $ tensJ g $ oppJ h

{- A morphism from [G] to [H] is then a winning strategy for [G âŠ¸ H]. -}

MorphJ : GameJ â„“ â†’ GameJ â„“ â†’ ğ’° â„“
MorphJ g h = wstratJ (homJ g h)

{- The happens-before game -}

bef : GameJ â„“ â†’ GameJ â„“ â†’ GameJ â„“
bef (done _)   h = h
bef (sigma gk) h = sigma Î» x â†’ bef (gk x) h
bef (pi gk)    h = pi Î» x â†’ bef (gk x) h
