module Games.Simple where

open import Prelude

open import Meta.Record

open import Data.Unit
open import Data.Empty

{- Dialectica as games in normal form -}

private variable
  â„“ : Level

{- Dialectica category -}

{- Objects -}

{- We look at Dialectica objects as games in normal form.
    They define a set of strategies, a set of counter-strategies,
    and a criterion for winning strategy profiles. -}

record Game (â„“ : Level) : ğ’° (â„“suc â„“) where
  constructor G
  field
    strat   : ğ’° â„“
    costrat : ğ’° â„“
    win     : strat â†’ costrat â†’ Prop â„“

open Game

{- Winning strategies -}

wstrat : Game â„“ â†’ ğ’° â„“
wstrat Î± = Î£[ u ê‰ Î± .strat ] ((x : Î± .costrat) â†’ âŒ Î± .win u x âŒŸ)

ws : {Î± : Game â„“} â†’ wstrat Î± â†’ strat Î±
ws x = x .fst

{- Morphisms -}

record Morph {â„“ : Level} (Î± Î² : Game â„“) : ğ’° â„“ where
  constructor M
  field
    fw        : strat Î± â†’ strat Î²
    bw        : strat Î± â†’ costrat Î² â†’ costrat Î±
    is-better : (u : strat Î±) (y : costrat Î²)
              â†’ âŒ Î± .win u (bw u y) âŒŸ â†’ âŒ Î² .win (fw u) y âŒŸ

unquoteDecl morph-iso = declare-record-iso morph-iso (quote Morph)

open Morph

{- Extensional equality -}

mext : {Î± Î² : Game â„“} {f g : Morph Î± Î²}
     â†’ ((u : Î± .strat)                 â†’ f .fw u   ï¼ g .fw u  )
     â†’ ((u : Î± .strat) (y : costrat Î²) â†’ f .bw u y ï¼ g .bw u y)
     â†’ f ï¼ g
mext {Î±} {Î²} {f} {g} fwe bwe =
  apË¢ {A = (Î£[ fw ê‰ (Î± .strat â†’ Î² .strat) ]
            Î£[ bw ê‰ (Î± .strat â†’ Î² .costrat â†’ Î± .costrat) ]
            ((u : Î± .strat) (y : Î² .costrat) â†’ âŒ Î± .win u (bw u y) âŒŸ â†’ âŒ Î² .win (fw u) y âŒŸ))}
      {B = Morph Î± Î²}
      (â‰…â†’â‰ƒ morph-iso â»Â¹ $_)
      (Î£-path (fun-ext fwe) $
       Î£-path (transport-refl (f .bw) âˆ™ fun-ext (fun-ext âˆ˜ bwe)) $
       fun-ext Î» u â†’ fun-ext Î» y â†’ fun-ext Î» c â†’ hlevel 1 _ (g .is-better u y c))

{- Compositional structure -}

idm : {Î± : Game â„“} â†’ Morph Î± Î±
idm = M id (Î» _ â†’ id) (Î» _ _ â†’ id)

compose : {Î± Î² Î³ : Game â„“} â†’ Morph Î± Î² â†’ Morph Î² Î³ â†’ Morph Î± Î³
compose f g =
 M (g .fw âˆ˜ f .fw)
   (Î» u â†’ f .bw u âˆ˜ g .bw (f. fw u))
   Î» u y â†’ g .is-better (f .fw u) y âˆ˜ f .is-better u (g .bw (f .fw u) y)

compose-id-l : {Î± Î² : Game â„“} â†’ (f : Morph Î± Î²)
             â†’ compose idm f ï¼ f
compose-id-l f = mext (Î» _ â†’ refl) Î» _ _ â†’ refl

compose-id-r : {Î± Î² : Game â„“} â†’ (f : Morph Î± Î²)
             â†’ compose f idm ï¼ f
compose-id-r f = mext (Î» _ â†’ refl) Î» _ _ â†’ refl

compose-assoc : {Î± Î² Î³ Î´ : Game â„“} â†’ (f : Morph Î± Î²) â†’ (g : Morph Î² Î³) â†’ (h : Morph Î³ Î´)
              â†’ compose f (compose g h) ï¼ compose (compose f g) h
compose-assoc f g h = mext (Î» _ â†’ refl) Î» _ _ â†’ refl

{- Tensor product -}

one : Game â„“
one {â„“} .strat   = Lift â„“ âŠ¤
one {â„“} .costrat = Lift â„“ âŠ¤
one {â„“} .win _ _ = el! (Lift â„“ âŠ¤)

tens : {X : ğ’° â„“} â†’ (X â†’ Game â„“) â†’ Game â„“
tens A .strat   = âˆ€ x â†’ A x .strat
tens A .costrat = âˆ€ x â†’ A x .costrat
tens A .win u y = el! (âˆ€ x â†’ âŒ A x. win (u x) (y x) âŒŸ) -- TODO construct vs Pi/Universal ?

tensm : {X : ğ’° â„“} {A B : X â†’ Game â„“}
      â†’ ((x : X) â†’ Morph (A x) (B x))
      â†’ Morph (tens A) (tens B)
tensm f .fw u            = Î» x â†’ f x .fw (u x)
tensm f .bw u y          = Î» x â†’ f x .bw (u x) (y x)
tensm f .is-better u y c = Î» x â†’ f x .is-better (u x) (y x) (c x)

{- Internal hom -}

hom : Game â„“ â†’ Game â„“ â†’ Game â„“
hom Î± Î² .strat          = Î± .strat â†’ Î² .strat Ã— (Î² .costrat â†’ Î± .costrat)
hom Î± Î² .costrat        = Î± .strat Ã— Î² .costrat
hom Î± Î² .win vx (u , y) = el! (âŒ Î± .win u (vx u .snd y) âŒŸ â†’ âŒ Î² .win (vx u .fst) y âŒŸ)

homm : {A1 A2 B1 B2 : Game â„“} (f : Morph A2 A1) (g : Morph B1 B2) â†’ Morph (hom A1 B1) (hom A2 B2)
homm f g .fw        = Î» u y â†’
                      let b2a2 = u (f .fw y) in
                      g .fw (b2a2 .fst) , f .bw y âˆ˜ b2a2 .snd âˆ˜ g .bw (b2a2 .fst)
homm f g .bw        = Î» u y â†’ (f .fw (y .fst)) , (g .bw (u (f .fw (y .fst)) .fst) (y .snd))
homm f g .is-better = Î» u y a1w a2w â†’
                      g .is-better (u (f .fw (y .fst)) .fst) (y .snd)
                        (a1w (f .is-better (y .fst) (u (f .fw (y .fst)) .snd (g .bw (u (f .fw (y .fst)) .fst) (y .snd))) a2w))

{- Cartesian product -}

top : Game â„“
top {â„“} .strat   = Lift â„“ âŠ¤
top {â„“} .costrat = Lift â„“ âŠ¥
top     .win u v = absurd (lower v)

prod : {X : ğ’° â„“} â†’ (X â†’ Game â„“) â†’ Game â„“
prod     A .strat          = âˆ€ x â†’ A x .strat
prod {X} A .costrat        = Î£[ x ê‰ X ] A x .costrat 
prod     A .win u (x , cx) = A x .win (u x) cx

prodm : {X : ğ’° â„“} {A B : X â†’ Game â„“}
      â†’ ((x : X) â†’ Morph (A x) (B x))
      â†’ Morph (prod A) (prod B)
prodm f .fw u                 = Î» x â†’ f x .fw (u x)
prodm f .bw u (x , cx)        = x , f x .bw (u x) cx
prodm f .is-better u (x , cx) = f x .is-better (u x) cx
